buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE")
    }
}

apply plugin: 'org.springframework.boot'


jar{
    baseName = 'wlpt-web'
    version = '0.1.0'
}

sourceSets{
    //集成测试相关配置
    integration_test {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir 'src/integration_test/java'
        }
        resources.srcDir file('src/integration_test/resources')
    }
}

configurations {
    integration_testCompile.extendsFrom testCompile
    integration_testRuntime.extendsFrom testRuntime
    javaAgent
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integration_test.output.classesDir
    classpath = sourceSets.integration_test.runtimeClasspath
    group = "verification"
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

def aspectJLTW = {
    //为了进行spring LTW 织入，添加jvmArgs
    def args = []
    configurations.javaAgent.files.each {
        args.add "-javaagent:${it}"
    }

    //远程调试用，启用aspectJ后，直接debug无法进入断点，必须启用远程调试
    args.add("-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005")
    jvmArgs =  args

    println jvmArgs
}

dependencies {
    compile(
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-security",
            project(":core")
    )

    javaAgent(
            "org.springframework:spring-instrument:4.3.9.RELEASE",
            "org.aspectj:aspectjweaver:1.8.9"
    )

    testCompile(
            "org.springframework.boot:spring-boot-starter-test",
            "org.springframework.security:spring-security-test",
            files("libs/GroboTestingJUnit-1.2.1-core.jar"),
    )
}